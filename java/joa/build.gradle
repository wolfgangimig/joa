import org.gradle.plugins.ide.eclipse.model.AccessRule

// ITJ-87: support JDK 13
plugins {
  id 'org.openjfx.javafxplugin' version '0.0.8'
}
javafx {
    version = "13"
    modules = [ 'javafx.controls', 'javafx.swing' ]
    
    // In order to start an add-in in Eclispe, follow https://openjfx.io/openjfx-docs/ 
    // section "JavaFX and Eclipse": add as JVM arguments:
    // --module-path "\path\to\javafx-sdk-13\lib" --add-modules javafx.controls,javafx.swing
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 13

group = 'com.wilutions.joa'

// Version number
// Also stored in joa/res/com/wilutions/com/version.properties 
version = '1.0.5.0'

archivesBaseName = 'JOA'



sourceSets {
    main {
        java {
            srcDir 'src'
            srcDir 'src-gen'
        }
        resources {
            srcDir 'native'
            srcDir 'res'
        }
    }
}

eclipse {
    classpath {
        file {
            whenMerged {
                def jre = entries.find { it.path.contains 'org.eclipse.jdt.launching.JRE_CONTAINER' }
                jre.accessRules.add(new AccessRule('0', 'javafx/**'))
                jre.accessRules.add(new AccessRule('0', 'sun/awt/windows/**'))
            }
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Quickstart',
                   'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
	compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
}

test {
    systemProperties 'property': 'value'
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            
            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
}

uploadArchives {
    repositories {
    	mavenDeployer {
    	
    		beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
    		
    		repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        		authentication(userName: ossrhUsername, password: ossrhPassword)
      		}

      		snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        		authentication(userName: ossrhUsername, password: ossrhPassword)
      		}
    		
		    pom.project {
		        name 'JOA'
		        packaging 'jar'
		        
		        // optionally artifactId can be defined here 
		        description 'Java Library for Micosoft Office Add-ins'
		        url 'https://www.wilutions.com'
		
		        scm {
		          connection 'scm:https://github.com/wolfgangimig/joa'
		          developerConnection 'scm:https://github.com/wolfgangimig/joa'
		          url 'https://github.com/wolfgangimig/joa'
		        }
		
		        licenses {
		          license {
		            name 'The MIT License (MIT)'
		            url 'https://raw.githubusercontent.com/wolfgangimig/joa/master/LICENSE.md'
		          }
		        }
		
		        developers {
		          developer {
		            id 'wimix'
		            name 'Wolfgang Imig'
		            email 'wolfgang.imig@googlemail.com'
		          }
		        }
		      }
		      
		      
		      
       		flatDir {
           		dirs 'repos'
       		}
       	}
    }
}
